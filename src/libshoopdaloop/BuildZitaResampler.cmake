include(ExternalProject)

set(ZITA_SOURCE_DIR ${CMAKE_SOURCE_DIR}/../third_party/zita-resampler)
set(ZITA_BUILD_DIR ${CMAKE_CURRENT_BINARY_DIR}/zita-resampler-build)
set(ZITA_TOPLEVEL_INCLUDE_DIR ${ZITA_SOURCE_DIR}/source)
set(ZITA_BUILD_TARGETS zita-resampler)

if(WIN32)
set(ZITA_LIB ${ZITA_BUILD_DIR}/zita-resampler.lib)
else()
set(ZITA_LIB ${ZITA_BUILD_DIR}/libzita-resampler.a)
endif()
set(ZITA_OUTPUTS ${ZITA_LIB})

set(CMAKE_BUILD_TARGET_ARGS)
foreach(item ${ZITA_BUILD_TARGETS})
    list(APPEND CMAKE_BUILD_TARGET_ARGS --target ${item})
endforeach()

set(ZITA_CMAKE_ARGS
  -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
  -DCMAKE_POSITION_INDEPENDENT_CODE=ON
)

if(WIN32)
  # Prevent redefinition when using pthread-win32
  set(ZITA_CMAKE_ARGS ${ZITA_CMAKE_ARGS} -DCMAKE_CXX_FLAGS=-DHAVE_STRUCT_TIMESPEC)
endif()

ExternalProject_Add(zita_proj
  CMAKE_ARGS ${ZITA_CMAKE_ARGS}
  BUILD_COMMAND cmake --build <BINARY_DIR> ${CMAKE_BUILD_TARGET_ARGS}
  INSTALL_COMMAND ""
  SOURCE_DIR ${ZITA_SOURCE_DIR}
  BINARY_DIR ${ZITA_BUILD_DIR}
  BUILD_BYPRODUCTS ${ZITA_OUTPUTS}
)

set(ZITA_LIB ${ZITA_LIB} PARENT_SCOPE)
set(ZITA_INCLUDE_DIR ${ZITA_TOPLEVEL_INCLUDE_DIR} PARENT_SCOPE)